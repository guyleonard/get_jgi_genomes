#!/usr/bin/env perl
use strict;
use warnings;

use Cwd;
use File::Basename;
use File::Path qw(make_path);
use Getopt::Std;
use List::MoreUtils qw(uniq);
use open qw(:std :utf8);
use utf8;
use XML::LibXML;

#
# Global Variables
#
my $cookies = 'cookies';
my $jgi_id  = 'false';
my $list    = 'false';
my $quiet   = 'false';
my ( $username, $password, $outdir, $project );

#
# Getopt
#
my %options = ();
getopts( 'u:p:c:fam:P:i:lhq', \%options ) or display_help();

# Help
if ( $options{h} ) { display_help(); }

# Quiet
if ( $options{q} ) { $quiet = "true"; }

# Signin Options / Cookies
if ( defined $options{u} && defined $options{p} ) {
    $username = "$options{u}";
    $password = "$options{p}";
    signin( $username, $password );
}
elsif ( defined $options{c} ) {
    $cookies = $options{c};
    print "INFO: User supplied cookie, skipping signin process.\n";
}
else {
    display_help();
}

# JGI ID
if ( defined $options{i} ) {
    $jgi_id = "$options{i}";
}

# List Only
if ( $options{l} ) {
    $list = "true";
    print "INFO: Output List Only\n";
}

# Download Project XML & Create Output Dir
# Fungi
if ( defined $options{f} ) {
    $project = 'fungi';

    print "INFO: User Selected Mycocosm aka $project\n";
    download_xml($project);

    print "INFO: Parsing XML\n";
    parse_cosm_xml( $project, $list, $jgi_id );
}

# Algae
elsif ( defined $options{a} ) {
    $project = 'algae';

    print "INFO: User Selected Phycocosm aka $project\n";
    download_xml($project);

    print "INFO: Parsing XML\n";
    parse_cosm_xml( $project, $list );
}

# Plants
elsif ( defined $options{P} ) {
    $project = 'PhytozomeV' . "$options{P}";

    print "INFO: User Selected Phytozome aka $project\n";
    download_xml($project);

    print "INFO: Parsing XML\n";
    parse_zome_xml( $project, $list );
}

# Animals
elsif ( defined $options{m} ) {
    $project = 'MetazomeV' . "$options{m}";

    print "INFO: User Selected Metazome aka $project\n";
    download_xml($project);

    print "INFO: Parsing XML\n";
    parse_zome_xml( $project, $list );
}
else {
    print "WARN: You must define a portal/cosm.\n";
    exit(1);
}

#
# Signon & Cookies
#
sub signin {

    my $user = shift;
    my $pass = shift;

    if ( -e 'cookies' ) {
        if ( -A 'cookies' > 1 || -M 'cookies' > 1 ) {
            print
                "INFO: It has been longer than one day since your last login.\nAttempting to login again...\n";
            run_cmd(
                "curl --silent 'https://signon.jgi.doe.gov/signon/create' --data-urlencode 'login=$user' --data-urlencode 'password=$pass' -c cookies > /dev/null",
                "$quiet"
            );
            check_cookie();
        }
        else {
            print
                "INFO: You are already logged in...please use the '-c' option.\n";
        }
    }
    else {
        print "INFO: Attempting to login...\n";
        run_cmd(
            "curl --silent 'https://signon.jgi.doe.gov/signon/create' --data-urlencode 'login=$user' --data-urlencode 'password=$pass' -c cookies > /dev/null",
            "$quiet"
        );
        check_cookie();
    }
}

sub check_cookie {

    my $logged_in = 'flase';

    open my $cookie, '<', "cookies" or die "Cannot open cookies file: $!\n";
    while (<$cookie>) {
        my $line = $_;
        if ( $line =~ m/TRUE/ ) {
            $logged_in = 'true';
        }
    }

    if ( $logged_in eq 'true' ) {
        print "INFO: Login Successfull!\n";
    }
    else {
        print
            "WARN: There was a problem with your login attempt. Please Try again.\n";
        exit(1);
    }
}

#
# XML Download
#
sub download_xml {
    my $portal = shift;

    if ( -e "$portal\_files.xml" ) {
        if (   -A "$portal\_files.xml" > '10'
            || -M "$portal\_files.xml" > '10' )
        {
            print
                "\tWarning: $portal\_files.xml found and is greater than 10 days old. Continuing, but you may wish to stop, delete and try again.\n";
        }
        else {
            print
                "Info: $portal\_files.xml found and is less than 10 days old. Continuing.\n";
        }
    }
    elsif ( !-e "$portal\_files.xml" ) {

        # Get portal List
        print "Downloading $portal XML - This may take a few minutes...\n";
        run_cmd(
            "curl 'https://genome.jgi.doe.gov/portal/ext-api/downloads/get-directory?organism=$portal' -b $cookies > $portal\_files.xml",
            "$quiet"
        );
    }

    # I can't get the XML parsing to work when "&quot;" exists in the file
    # let's cheat and remove it with sed
    run_cmd( "sed -i \'s/&quot;//g\' $portal\_files.xml", "true" );
}

#
# XML Parsing
#
# Mycocosm and Others
sub parse_cosm_xml {

    my ( $portal, $list, $jgi_id ) = @_;

    my $xml_file = "$portal\_files.xml";

    my $portal_xml
        = XML::LibXML->load_xml( location => $xml_file, no_blanks => 1 );

    my $query
        = '/organismDownloads[@name="'
        . $portal
        . '"]/folder[@name="Files"]/folder[@name="Annotation"]/folder[@name="Filtered Models (best)"]/folder[@name="Proteins"]';

    my %results;

    for my $file ( $portal_xml->findnodes($query) ) {
        for my $file_props ( $file->findnodes('./*') ) {

            my $label = $file_props->getAttribute('label');
            my $url   = $file_props->getAttribute('url');

            # if the files don't look like this, then we don't want them
            # Armlut1_GeneCatalog_proteins_20180531.aa.fasta.gz
            # *proteins.fasta.gz
            # we also remove any files with some modifiers
            # then push them to a hash to remove genomes with two sets
            # of proteins.
            # it's a bit of a fudge, but the xml makes no effort to define
            # the set of AAs that are representative of the genome

            if ( $url =~ m/.*\_GeneCatalog_proteins_\d+\.aa\.fasta.gz/ ) {
                if ( $url !~ m/primary|secondary|alleles|diploid|old/ ) {
                    $results{"$label"} = "$url";
                }
            }
            elsif ( $url =~ m/.*proteins\.fasta\.gz/ ) {
                $results{"$label"} = "$url";
            }
        }
    }

    if ( !-d $portal ) {
        make_path($portal);
    }

    if ( $list eq "true" ) {
        genome_list( \%results, $portal );
    }
    else {
        genome_list( \%results, $portal );
        download_files_cosms( \%results, $portal, $jgi_id );
    }
}

# Phytozome
sub parse_zome_xml {

    my ( $portal, $list ) = @_;

    my $xml_file = "$portal\_files.xml";

    my $portal_xml
        = XML::LibXML->load_xml( location => $xml_file, no_blanks => 1 );

    my $query = '//folder[@name="annotation"]';

    my %results;

    my $search = 'protein';
    if ( $portal =~ m/metazome/i ) { $search = 'peptide' }

    for my $file ( $portal_xml->findnodes($query) ) {
        for my $file_props ( $file->findnodes('//file') ) {

            my $url = $file_props->getAttribute('url');
            my $label = ( split /\//, $url )[9];
            if ( $url =~ m/.*$search\.fa\.gz/ ) {
                $results{"$label"} = "$url";
            }
        }
    }

    if ( !-d $portal ) {
        make_path($portal);
    }

    if ( $list eq "true" ) {

        genome_list_zome ( \%results, $portal );
    }

    else {
        genome_list_zome ( \%results, $portal );
        download_files_zome( \%results, $portal );
    }
}

#
# Generate List Output
#
sub genome_list {
    my %list_of_urls = %{ $_[0] };
    my $portal       = $_[1];

    my $filename = "$portal\/$portal\_list.txt";
    open my $fileout, '>>', $filename;

    foreach ( sort keys %list_of_urls ) {
        my $taxa   = $_;
        my $url    = $list_of_urls{$_};
        my @jgi_id = split /\//, $url;

        print $fileout
            "$taxa\t$jgi_id[2]\thttps://genome.jgi.doe.gov/portal/$jgi_id[2]/download/$jgi_id[4]\n";
    }

    close($fileout);
}

sub genome_list_zome  {
    my %list_of_urls = %{ $_[0] };
    my $portal       = $_[1];

    my $filename = "$portal\/$portal\_list.txt";
    open my $fileout, '>>', $filename;

    foreach ( sort keys %list_of_urls ) {
        my $taxa = $_;
        my $url  = $list_of_urls{$_};

        print $fileout "$taxa\thttps://genome.jgi.doe.gov$url\n";
    }

    close($fileout);
}

#
# Download Files
#
sub download_files_cosms {
    my %list_of_urls = %{ $_[0] };
    my $portal       = $_[1];
    my $jgi_id       = $_[2];

    if ( $jgi_id ne 'false' ) {
        foreach ( sort keys %list_of_urls ) {
            my $taxa     = $_;
            my $url      = $list_of_urls{$_};
            my $filename = ( split /\//, $url )[4];

            print "\tRetrieving: $filename\n";

            run_cmd(
                "curl --silent 'https://genome.jgi.doe.gov$url' -b cookies > $portal\/$filename",
                "$quiet"
            );
        }
    }
    else {

    }
}

sub download_files_zome {
    my %list_of_urls = %{ $_[0] };
    my $portal       = $_[1];

    foreach ( sort keys %list_of_urls ) {
        my $taxa     = $_;
        my $url      = $list_of_urls{$_};
        my $filename = ( split /\//, $url )[9];

        print "\tRetrieving: $filename\n";

        run_cmd(
            "curl --silent 'https://genome.jgi.doe.gov$url' -b cookies > $portal\/$filename",
            "$quiet"
        );
    }
}

#
# Helpers
#
sub run_cmd {
    my ( $cmd, $quiet ) = @_;
    msg("Running: $cmd") unless $quiet eq "true";
    system($cmd) == 0 or error("Error $? running command");
}

sub error {
    msg(@_);
    exit(1);
}

sub msg {
    print STDERR "@_\n";
}

#
# Help
#
sub display_help {
    print "Usage:\n";
    print
        "  get_jgi_genomes [-u <username> -p <password>] | [-c <cookies>] [-f | -a | -P 12 | -m 3] (-i) (-l) (-q)\n\n";
    print "Required:\n";
    print "\t-u <username>\n";
    print "\t-p <password>\n";
    print "or\n";
    print "\t-c <cookie file>\n";
    print "Database Options:\n";
    print "\t-f Mycocosm aka fungi\n";
    print "\t-a Phycocosm aka algae\n";
    print "\t-P <version> PhytozomeV aka plants\n";
    print "\t-m <metazome> MetazomeV aka metazoans\n";
    print "JGI ID:";
    print "\t-i <id> JGI ID of Genome Project\n";
    print "Other:";
    print "\t-l list only, no downloads\n";

    exit(1);
}
